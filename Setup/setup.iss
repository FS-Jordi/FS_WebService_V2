; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "FS License Server"
#define MyAppVersion "1.0"
#define MyAppPublisher "ArtecSoft, SL"
#define MyAppURL "https://www.artecsoft.com"
#define MyAppExeName "FS_LicenseServer.exe"
#define GuardianExeName "FS_ServerGuardian.exe"
#define GuardianAppName "FS Guardian"
#define TargetIniName "FS_LicenseServer.ini"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{22C6CD52-BECE-48C7-900C-AE019EFCA7C3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\FactoryStart\{#MyAppName}
DefaultGroupName=FS License Server
OutputDir=C:\ARTECSOFT\Projectes\Programes\SAGE\License server\Setup
OutputBaseFilename=install_licenseserver
SetupIconFile=C:\ARTECSOFT\Projectes\Programes\SAGE\License server\Source\FS_LicenseServer_Icon.ico
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin
ChangesEnvironment=yes

[CustomMessages]
CustomForm_Caption=Conectar a Servidor de BBDD
CustomForm_Description=Entre la información requerida para conectar al servidor de bases de datos
CustomForm_lblServer_Caption0=Nombre del servidor:
CustomForm_lblAuthType_Caption0=Credenciales
CustomForm_lblUser_Caption0=Nombre usuario:
CustomForm_lblPassword_Caption0=Contraseña:
CustomForm_lblDatabase_Caption0=Base de datos:
CustomForm_chkSQLAuth_Caption0=Usar autenticación de SQL Server
CustomForm_chkWindowsAuth_Caption0=Usar autenticación de Windows
CustomForm_lblVersion_Caption0=Versión
CustomForm_lstVersion_Line0=2008 R2
CustomForm_lstVersion_Line1=2012
CustomForm_lblIP_Caption0=IP y puertos UDP/TFTP:
CustomForm_lblArchivoLicencia=Archivo de licencia:

[Languages]
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Files]
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\FS_ServerGuardian.exe"; DestDir: "{app}"; Flags: ignoreversion 
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\FS_LicenseServer.exe"; DestDir: "{app}"; Flags: ignoreversion 
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\FS_LicenseServer.ini"; DestDir: "{app}"; Flags: ignoreversion; Permissions: users-modify;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\License.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\EncryptionDLL.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\libeay32.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\ssleay32.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\restore.cmd"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\update.cmd"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\updatelic.cmd"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\unzip.exe"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\startall.cmd"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\License server\stopall.cmd"; DestDir: "{app}"; Flags: ignoreversion;
Source: "C:\ARTECSOFT\Projectes\Programes\SAGE\TestLCOEM\testlcoem.exe"; DestDir: "{app}"; Flags: ignoreversion 
Source: "clean.sql"; DestDir: "{tmp}"; Flags: dontcopy
Source: "install.sql"; DestDir: "{tmp}"; Flags: dontcopy
;Source: "EncryptionDLL.dll"; Flags: dontcopy

[Registry]
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "FS_InstallPath"; ValueData: "{app}"; Flags: createvalueifdoesntexist preservestringtype

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\{#MyAppExeName}"; Parameters: "/install /silent"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: runascurrentuser runhidden
Filename: "{app}\{#GuardianExeName}"; Parameters: "/install /silent"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: runascurrentuser runhidden
Filename: {sys}\sc.exe; Parameters: "start FS_MainLicenseServer"; Flags: runascurrentuser
Filename: {sys}\sc.exe; Parameters: "start FSGuardian"; Flags: runascurrentuser
;Filename: "{cmd}"; Parameters: "/C set FS_InstallPath={app}"; Flags: postinstall runhidden

[UninstallRun]
Filename: {sys}\sc.exe; Parameters: "stop FSGuardian"; Flags: runascurrentuser runhidden
Filename: {sys}\sc.exe; Parameters: "stop FS_MainLicenseServer"; Flags: runascurrentuser runhidden
Filename: "{app}\{#GuardianExeName}"; Parameters: "/uninstall /silent"; Flags: runhidden runascurrentuser
Filename: "{app}\{#MyAppExeName}"; Parameters: "/uninstall /silent"; Flags: runhidden runascurrentuser

[INI]
Filename: {app}\FS_LicenseServer.ini; Section: "DB"; Key: "Provider"; String: "SQLOLEDB"
Filename: {app}\FS_LicenseServer.ini; Section: "DB"; Key: "Host"; String: {code:getHost}
Filename: {app}\FS_LicenseServer.ini; Section: "DB"; Key: "BBDD"; String: {code:getBBDD}
Filename: {app}\FS_LicenseServer.ini; Section: "DB"; Key: "User"; String: {code:getUser}
Filename: {app}\FS_LicenseServer.ini; Section: "DB"; Key: "Password"; String: {code:getPassword}
Filename: {app}\FS_LicenseServer.ini; Section: "UDP"; Key: "Host"; String: {code:getIP}
Filename: {app}\FS_LicenseServer.ini; Section: "UDP"; Key: "Port"; String: {code:getPort}
Filename: {app}\FS_LicenseServer.ini; Section: "UDP"; Key: "FTPPort"; String: {code:getFTPPort}
Filename: {app}\FS_LicenseServer.ini; Section: "GENERAL"; Key: "RestartService"; String: "1"
Filename: {app}\FS_LicenseServer.ini; Section: "GENERAL"; Key: "RestartServiceTime"; String: "03:00"
Filename: {app}\FS_LicenseServer.ini; Section: "ALBARANCLIENTE"; Key: "Formato"; String: "Y/SN"
Filename: {app}\FS_LicenseServer.ini; Section: "ALBARANCLIENTE"; Key: "FormatoEjercicio"; String: "%0.4d"
Filename: {app}\FS_LicenseServer.ini; Section: "ALBARANCLIENTE"; Key: "FormatoSerie"; String: "%s"
Filename: {app}\FS_LicenseServer.ini; Section: "ALBARANCLIENTE"; Key: "FormatoNumero"; String: "%d"

[Code]
function Encrypt ( str: AnsiString ): AnsiString;
external 'Encrypt@files:EncryptionDLL.dll stdcall delayload setuponly';

function Decrypt ( str: AnsiString ): AnsiString; 
external 'Decrypt@files:EncryptionDLL.dll stdcall delayload setuponly';

function DecryptU ( str: AnsiString ): AnsiString; 
external 'Decrypt@{app}\EncryptionDLL.dll stdcall delayload uninstallonly';
 
const
  adCmdUnspecified = $FFFFFFFF;
  adCmdUnknown = $00000008;
  adCmdText = $00000001;
  adCmdTable = $00000002;
  adCmdStoredProc = $00000004;
  adCmdFile = $00000100;
  adCmdTableDirect = $00000200;
  adOptionUnspecified = $FFFFFFFF;
  adAsyncExecute = $00000010;
  adAsyncFetch = $00000020;
  adAsyncFetchNonBlocking = $00000040;
  adExecuteNoRecords = $00000080;
  adExecuteStream = $00000400;
  adExecuteRecord = $00000800;

var
  lblServer: TLabel;
  lblAuthType: TLabel;
  lblUser: TLabel;
  lblPassword: TLabel;
  lblDatabase: TLabel;
  chkSQLAuth: TRadioButton;
  txtServer: TEdit;
  lblIP: TLabel;
  txtIP: TEdit;
  txtPort: TEdit;
  txtFTPPort: TEdit;
  chkWindowsAuth: TRadioButton;
  txtUsername: TEdit;
  txtPassword: TPasswordEdit;
  lstDatabase: TComboBox;
  bIsNextEnabled: Boolean;
  lblArchivoLicencia: TLabel;
  txtArchivoLicencia: TEdit;
  btnArchivoLicencia: TButton;

 var
  Page: TWizardPage;

{ Used to generate error code by sql script errors }
procedure ExitProcess(exitCode:integer);
  external 'ExitProcess@kernel32.dll stdcall';

procedure SelectLicenseFile (Sender:TObject);
var
  FileName: String;
begin
  // Set the initial filename
  FileName := '';
  if GetOpenFileName('', FileName, '',
     'Text Documents (*.dat)|*.dat|All Files|*.*', 'dat') then
  begin
    // Successful; user clicked OK
    // FileName contains the selected filename
    txtArchivoLicencia.Text := FileName;
  end;
end;

{ enable/disable child text boxes & functions when text has been entered into Server textbox. Makes no sense to populate child items unless a value exists for server. }
Procedure ServerOnChange (Sender: TObject);
begin                            
  lstDatabase.Items.Clear;
  lstDatabase.Text := '';
  bIsNextEnabled := False;
  WizardForm.NextButton.Enabled := bIsNextEnabled;
  if Length(txtServer.Text) > 0 then
  begin
    lblAuthType.Enabled := True;
    lblDatabase.Enabled := True;
    lstDatabase.Enabled := True;
    chkWindowsAuth.Enabled := True;
    chkSQLAuth.Enabled := True;
  end
  else
  begin
    lblAuthType.Enabled := False;
    lblDatabase.Enabled := False;
    lstDatabase.Enabled := False; 
    chkWindowsAuth.Enabled := False;
    chkSQLAuth.Enabled := False;
  end
end;

{ enable/disable user/pass text boxes depending on selected auth type. A user/pass is only required for SQL Auth }
procedure  AuthOnChange (Sender: TObject);
begin
  if chkSQLAuth.Checked then
  begin
    lblUser.Enabled := true;
    lblPassword.Enabled := true;
    txtUsername.Enabled := true;
    txtPassword.Enabled := true;
  end
  Else
  begin
    lblUser.Enabled := false;
    lblPassword.Enabled := false;
    txtUsername.Enabled := false;
    txtPassword.Enabled := false;
  end
end;

{ Enable next button once a database name has been entered. }
Procedure DatabaseOnChange (Sender: TObject);
begin
  if (Length(lstDatabase.Text) > 0) and (lstDatabase.Enabled) then
  begin
    bIsNextEnabled := True;
    WizardForm.NextButton.Enabled := bIsNextEnabled;  
  end
  else
  begin
    bIsNextEnabled := False;
    WizardForm.NextButton.Enabled := bIsNextEnabled;  
  end
end;

{ Retrieve a list of databases accessible on the server with the credentials specified. }
{ This list is shown in the database dropdown list }
procedure RetrieveDatabaseList(Sender: TObject);
var  
  ADOCommand: Variant;
  ADORecordset: Variant;
  ADOConnection: Variant;  
begin
  lstDatabase.Items.Clear;
  try
    { create the ADO connection object }
    ADOConnection := CreateOleObject('ADODB.Connection');
    { build a connection string; for more information, search for ADO }
    { connection string on the Internet  }
    ADOConnection.ConnectionString := 
      'Provider=SQLOLEDB;' +               { provider }
      'Data Source=' + txtServer.Text + ';' +   { server name }
      'Application Name=' + '{#SetupSetting("AppName")}' + ' DB List;'
    if chkWindowsAuth.Checked then
      ADOConnection.ConnectionString := ADOConnection.ConnectionString +
      'Integrated Security=SSPI;'         { Windows Auth }
    else
      ADOConnection.ConnectionString := ADOConnection.ConnectionString +
      'User Id=' + txtUsername.Text + ';' +              { user name }
      'Password=' + txtPassword.Text + ';';                   { password }
    { open the connection by the assigned ConnectionString }
    ADOConnection.Open;
    try
      { create the ADO command object }
      ADOCommand := CreateOleObject('ADODB.Command');
      { assign the currently opened connection to ADO command object }
      ADOCommand.ActiveConnection := ADOConnection;
      { assign text of a command to be issued against a provider }
      ADOCommand.CommandText := 'SELECT name FROM master.dbo.sysdatabases WHERE HAS_DBACCESS(name) = 1 ORDER BY name';
      { this property setting means, that you're going to execute the }
      { CommandText text command; it does the same, like if you would }
      { use only adCmdText flag in the Execute statement }
      ADOCommand.CommandType := adCmdText;
      { this will execute the command and return dataset }
      ADORecordset := ADOCommand.Execute;
      { get values from a dataset using 0 based indexed field access; }
      { notice, that you can't directly concatenate constant strings }
      { with Variant data values }
      while not ADORecordset.eof do 
      begin
       lstDatabase.Items.Add(ADORecordset.Fields(0));
       ADORecordset.MoveNext;
      end;

    finally
      ADOConnection.Close;
    end;
  except
    MsgBox(GetExceptionMessage, mbError, MB_OK);
  end;
end;

{ Execute files specified in [files] section (hardcoded) against the user defined server.database }
procedure DeploySQL();
var  
  SQLScript: AnsiString;    
  ADOCommand: Variant;
  ADOConnection: Variant;  
begin

  try
    { create the ADO connection object }
    ADOConnection := CreateOleObject('ADODB.Connection');
    { build a connection string; for more information, search for ADO }
    { connection string on the Internet }
    ADOConnection.ConnectionString := 
      'Provider=SQLOLEDB;' +
      'Data Source=' + txtServer.Text + ';' +
      'Initial Catalog=' + lstDatabase.Text + ';' +
      'Application Name=' + '{#SetupSetting("AppName")}' + ' Execute SQL;' ;     
    if chkWindowsAuth.Checked then
      ADOConnection.ConnectionString := ADOConnection.ConnectionString +
      'Integrated Security=SSPI;'        
    else
      ADOConnection.ConnectionString := ADOConnection.ConnectionString +
      'User Id=' + txtUsername.Text + ';' +            
      'Password=' + txtPassword.Text + ';';  
    { open the connection by the assigned ConnectionString }
    ADOConnection.Open;
    try
      { create the ADO command object }
      ADOCommand := CreateOleObject('ADODB.Command');
      { assign the currently opened connection to ADO command object }
      ADOCommand.ActiveConnection := ADOConnection;
      { load a script from file into variable. Exclusive OR because both versions should never exist at the same time. }
        ExtractTemporaryFile('install.sql');
        if (LoadStringFromFile(ExpandConstant('{tmp}\install.sql'), SQLScript)) then
      begin
        { assign text of a command to be issued against a provider. Append all 3 because one of the install assembly strings will always be empty. }
        ADOCommand.CommandText := SQLScript;
        { this will execute the script; the adCmdText flag here means }
        { you're going to execute the CommandText text command, while }
        { the adExecuteNoRecords flag ensures no data row will be get }
        { from a provider, what should improve performance }
        ADOCommand.Execute(NULL, NULL, adCmdText or adExecuteNoRecords);
      end
      else
      begin
        MsgBox('Installation files missing.', mbError, MB_OK);
        ExitProcess(7);
      end;
    finally
      ADOConnection.Close;
    end;
  except
    MsgBox(GetExceptionMessage, mbError, MB_OK);
    ExitProcess(5);
  end;
end;


{ Execute files specified in [files] section (hardcoded) against the user defined server.database }
procedure DeploySQLUninstall();
var  
  SQLScript: AnsiString;    
  ADOCommand: Variant;
  ADOConnection: Variant;  
  sServer: AnsiString;
  sUser: AnsiString;
  sBBDD: AnsiString;
  sPass: AnsiString;
  sProvider: AnsiString;
begin

  sProvider := GetIniString('DB', 'Provider', 'SQLNCLI10.1', ExpandConstant('{app}') + '\FS_LicenseServer.ini');
  sServer   := GetIniString('DB', 'Host', '', ExpandConstant('{app}') + '\FS_LicenseServer.ini');
  sBBDD     := GetIniString('DB', 'BBDD', '', ExpandConstant('{app}') + '\FS_LicenseServer.ini');
  sUser     := GetIniString('DB', 'User', '', ExpandConstant('{app}') + '\FS_LicenseServer.ini');
  sPass     := GetIniString('DB', 'Password', '', ExpandConstant('{app}') + '\FS_LicenseServer.ini');
  sPass     := DecryptU ( sPass );

  try
    { create the ADO connection object }
    ADOConnection := CreateOleObject('ADODB.Connection');
    { build a connection string; for more information, search for ADO }
    { connection string on the Internet }
    ADOConnection.ConnectionString := 
      'Provider=SQLOLEDB;' +
      'Data Source=' + sServer + ';' +
      'Initial Catalog=' + sBBDD + ';' +
      'User Id=' + sUser + ';' +            
      'Password=' + sPass + ';' +
      'Application Name=' + '{#SetupSetting("AppName")}' + ' Execute SQL;' ;     
    { open the connection by the assigned ConnectionString }
    ADOConnection.Open;
    try
      { create the ADO command object }
      ADOCommand := CreateOleObject('ADODB.Command');
      { assign the currently opened connection to ADO command object }
      ADOCommand.ActiveConnection := ADOConnection;
      { load a script from file into variable. Exclusive OR because both versions should never exist at the same time. }
      SQLScript := 'IF OBJECT_ID(''dbo.FS_WorkerPermission'', ''U'') IS NOT NULL ' +
                  'DROP TABLE dbo.FS_WorkerPermission; ' +
                  'IF OBJECT_ID(''dbo.FS_Worker'', ''U'') IS NOT NULL ' + 
                  '  DROP TABLE dbo.FS_Worker; ' +
                  'IF OBJECT_ID(''dbo.FS_Terminal'', ''U'') IS NOT NULL  ' +
                  '  DROP TABLE dbo.FS_Terminal; ' +
                  'IF OBJECT_ID(''dbo.FS_Sinopticos_Detalle'', ''U'') IS NOT NULL  ' +
                  '  DROP TABLE dbo.FS_Sinopticos_Detalle; ' +
                  'IF OBJECT_ID(''dbo.FS_Sinopticos'', ''U'') IS NOT NULL  ' +
                  '  DROP TABLE dbo.FS_Sinopticos; ' +
                  'IF OBJECT_ID(''dbo.FS_Shapes'', ''U'') IS NOT NULL  ' +
                  '  DROP TABLE dbo.FS_Shapes; ' +
                  'IF OBJECT_ID(''dbo.FS_Permission'', ''U'') IS NOT NULL  ' +
                  '  DROP TABLE dbo.FS_Permission; ' +
                  'IF OBJECT_ID(''dbo.FS_Logintype'', ''U'') IS NOT NULL  ' +
                  '  DROP TABLE dbo.FS_Logintype; ' +
                  'IF OBJECT_ID(''dbo.FS_License_Detail'', ''U'') IS NOT NULL  ' +
                  '  DROP TABLE dbo.FS_License_Detail; ' +
                  'IF OBJECT_ID(''dbo.FS_License'', ''U'') IS NOT NULL  ' +
                  'DROP TABLE dbo.FS_License;';
        ADOCommand.CommandText := SQLScript;
        //ADOCommand.Execute(NULL, NULL, adCmdText or adExecuteNoRecords);
    finally
      ADOConnection.Close;
    end;
  except
    MsgBox(GetExceptionMessage, mbError, MB_OK);
    ExitProcess(5);
  end;

  

end;

{ CustomForm_NextkButtonClick }
{ try to connect to supplied db. Dont need to catch errors/close conn on error because a failed connection is never opened. }
function CustomForm_NextButtonClick(Page: TWizardPage): Boolean;
var  
  ADOConnection: Variant;  
begin
    { create the ADO connection object }
    ADOConnection := CreateOleObject('ADODB.Connection');
    { build a connection string; for more information, search for ADO }
    { connection string on the Internet }
    ADOConnection.ConnectionString := 
      'Provider=SQLOLEDB;' + 
      'Data Source=' + txtServer.Text + ';' + 
      'Initial Catalog=' + lstDatabase.Text + ';' +  
      'Application Name=' + '{#SetupSetting("AppName")}' + ' Execute SQL;' ;     
    if chkWindowsAuth.Checked then
      ADOConnection.ConnectionString := ADOConnection.ConnectionString +
      'Integrated Security=SSPI;'       
    else
      ADOConnection.ConnectionString := ADOConnection.ConnectionString +
      'User Id=' + txtUsername.Text + ';' +            
      'Password=' + txtPassword.Text + ';';         
    { open the connection by the assigned ConnectionString }
    ADOConnection.Open;
    Result := True;
end;

{ CustomForm_CreatePage }

function CustomForm_CreatePage(PreviousPageId: Integer): Integer;
begin

  Page := CreateCustomPage(
    PreviousPageId,
    ExpandConstant('{cm:CustomForm_Caption}'),
    ExpandConstant('{cm:CustomForm_Description}')
  );

  { Archivo de licencia }
  lblArchivoLicencia := TLabel.Create(Page);
  with lblArchivoLicencia do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_lblArchivoLicencia}');
    Left := ScaleX(24);
    Top := ScaleY(8+3);
    Width := ScaleX(98);
    Height := ScaleY(13);
    Enabled := True;
  end;

  txtArchivoLicencia := TEdit.Create(Page);
  with txtArchivoLicencia do
  begin
    Parent := Page.Surface;
    Left := ScaleX(142);
    Top := ScaleY(8);
    Width := ScaleX(252);
    Height := ScaleY(21);
    TabOrder := 0;
    Enabled := True;
    Text := '';
  end;

  btnArchivoLicencia := TButton.Create(Page);
  with btnArchivoLicencia do
  begin
    Parent := Page.Surface;
    Left := ScaleX(395);
    Top := ScaleY(8);
    Width := ScaleX(21);
    Height := ScaleY(21);
    TabOrder := 1;
    Enabled := True;
    Caption := '...';
    OnClick := @SelectLicenseFile;
  end;

  { lblServer }
  lblServer := TLabel.Create(Page);
  with lblServer do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_lblServer_Caption0}');
    Left := ScaleX(24);
    Top := ScaleY(32+3);
    Width := ScaleX(98);
    Height := ScaleY(13);
    Enabled := True;
  end;

  { txtServer }
  txtServer := TEdit.Create(Page);
  with txtServer do
  begin
    Parent := Page.Surface;
    Left := ScaleX(142);
    Top := ScaleY(32);
    Width := ScaleX(273);
    Height := ScaleY(21);
    TabOrder := 2;
    Enabled := True;
    Text := '';
    OnChange := @ServerOnChange;
  end;

  { lblAuthType }
  lblAuthType := TLabel.Create(Page);
  with lblAuthType do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_lblAuthType_Caption0}');
    Left := ScaleX(24);
    Top := ScaleY(72+3);
    Width := ScaleX(117);
    Height := ScaleY(13);
    Enabled := True;
  end;

  { chkWindowsAuth }
  chkWindowsAuth := TRadioButton.Create(Page);
  with chkWindowsAuth do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_chkWindowsAuth_Caption0}');
    Left := ScaleX(32);
    Top := ScaleY(88);
    Width := ScaleX(177);
    Height := ScaleY(17);
    Checked := False;
    TabOrder := 10;
    TabStop := True;
    OnClick := @AuthOnChange;
    Enabled := False;
  end;

  { chkSQLAuth }
  chkSQLAuth := TRadioButton.Create(Page);
  with chkSQLAuth do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_chkSQLAuth_Caption0}');
    Left := ScaleX(32);
    Top := ScaleY(108);
    Width := ScaleX(185);
    Height := ScaleY(17);
    Checked := True;
    TabOrder := 3;
    OnClick := @AuthOnChange;
    Enabled := True;
  end;

  { lblUser }
  lblUser := TLabel.Create(Page);
  with lblUser do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_lblUser_Caption0}');
    Left := ScaleX(56);
    Top := ScaleY(128+3);
    Width := ScaleX(88);
    Height := ScaleY(13);
    Enabled := False;
  end;

  { lblPassword }
  lblPassword := TLabel.Create(Page);
  with lblPassword do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_lblPassword_Caption0}');
    Left := ScaleX(56);
    Top := ScaleY(152+3);
    Width := ScaleX(83);
    Height := ScaleY(13);
    Enabled := False;
  end;

  { txtUsername }
  txtUsername := TEdit.Create(Page);
  with txtUsername do
  begin
    Parent := Page.Surface;
    Left := ScaleX(150);
    Top := ScaleY(128);
    Width := ScaleX(241);
    Height := ScaleY(21);
    Enabled := True;
    Text := 'logic';
    TabOrder := 4;
  end;

  { txtPassword }
  txtPassword := TPasswordEdit.Create(Page);
  with txtPassword do
  begin
    Parent := Page.Surface;
    Left := ScaleX(150);
    Top := ScaleY(152);
    Width := ScaleX(241);
    Height := ScaleY(21);
    Enabled := True;
    Text := 'Sage2009+';
    TabOrder := 5;
  end;

   { lblDatabase }
  lblDatabase := TLabel.Create(Page);
  with lblDatabase do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_lblDatabase_Caption0}');
    Left := ScaleX(56);
    Top := ScaleY(176+3);
    Width := ScaleX(83);
    Height := ScaleY(13);
    Enabled := False;
  end;

  { lstDatabase }
  lstDatabase := TComboBox.Create(Page);
  with lstDatabase do
  begin
    Parent := Page.Surface;
    Left := ScaleX(150);
    Top := ScaleY(176);
    Width := ScaleX(241);
    Height := ScaleY(21);
    Enabled := True;
    TabOrder := 6;    
    OnDropDown:= @RetrieveDatabaseList;
    OnChange:= @DatabaseOnChange;
  end;

  {lblIP}
  lblIP := TLabel.Create(Page);
  with lblIP do
  begin
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_lblIP_Caption0}');
    Left := ScaleX(56);
    Top := ScaleY(200+3);
    Width := ScaleX(123);
    Height := ScaleY(13);
    Enabled := False;
  end;

  {txtIP}
  txtIP := TEdit.Create(Page);
  with txtIP do
  begin
    Parent := Page.Surface;
    Left := ScaleX(150+34);
    Top := ScaleY(200);
    Width := ScaleX(100);
    Height := ScaleY(21);
    Enabled := True;
    Text := '';
    TabOrder := 7;
  end;

  {txtPort}
  txtPort := TEdit.Create(Page);
  with txtPort do
  begin
    Parent := Page.Surface;
    Left := ScaleX(255+34);
    Top := ScaleY(200);
    Width := ScaleX(50);
    Height := ScaleY(21);
    Enabled := True;
    Text := '18755';
    TabOrder := 8;
  end;

  {txtFTPPort}
  txtFTPPort := TEdit.Create(Page);
  with txtFTPPort do
  begin
    Parent := Page.Surface;
    Left := ScaleX(307+34);
    Top := ScaleY(200);
    Width := ScaleX(50);
    Height := ScaleY(21);
    Enabled := True;
    Text := '69';
    TabOrder := 9;
  end;

  with Page do
  begin
    OnNextButtonClick := @CustomForm_NextButtonClick;
  end;

  Result := Page.ID;
end;

procedure CurPageChanged(CurPageID: Integer);
begin
{ set initial status of next button. Should be disabled when page is first loaded, but should be enabled if user clicked back. }
  if CurPageID = Page.ID then
    WizardForm.NextButton.Enabled := bIsNextEnabled;  
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
{ The preinstall step seems like the best time to do the actual install. The problem is that this is not a traditional install. Nothing is copied to the users' pc }
  if CurStep = ssInstall then begin
    DeploySQL;
  end;

  if CurStep = ssPostInstall then begin   
    CreateDir ( ExpandConstant('{app}\updates') );
    if FileExists(txtArchivoLicencia.Text) then 
    begin   
      FileCopy(txtArchivoLicencia.Text, ExpandConstant('{app}\license.dat'), False);
    end;
  end;

end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then
  begin
    DeploySQLUninstall();
    UnloadDLL(ExpandConstant('{app}\EncryptionDLL.dll'));
  end;
  if CurUninstallStep = usPostUninstall then
  begin
    // Delete the directory C:\Test and everything inside it
    DelTree(ExpandConstant('{app}'), True, True, True);
  end;
end;

procedure InitializeWizard();
begin
  bIsNextEnabled := False;
  CustomForm_CreatePage(wpLicense);
end;

function getHost ( Param: String ): String;
begin
  Result := txtServer.Text;
end;

function getBBDD ( Param: String ): String;
begin
  Result := lstDatabase.Text;
end;

function getUser ( Param: String ): String;
begin
  Result := txtUsername.Text;
end;

function getPassword ( Param: String ): String;
begin
  Result := Encrypt(txtPassword.Text);
end;

function getIP ( Param: String ): String;
begin
  Result := txtIP.Text;
end;

function getPort ( Param: String ): String;
begin
  Result := txtPort.Text;
end;

function getFTPPort ( Param: String ): String;
begin
  Result := txtFTPPort.Text;
end;

function getRestartService ( Param: String ): String;
begin
  Result := txtFTPPort.Text;
end;

begin
end.
